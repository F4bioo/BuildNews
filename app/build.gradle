import io.gitlab.arturbosch.detekt.Detekt

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}
apply from: "$rootDir/plugins/android-build.gradle"
apply from: "$rootDir/plugins/flavor-build.gradle"
apply from: "$rootDir/config/detekt/detekt-build.gradle"
apply from: "$rootDir/plugins/jacoco-build.gradle"

def keyApiPropertiesFile = rootProject.file("$rootDir/keyApi.properties")
def keyApiProperties = new Properties()
keyApiProperties.load(new FileInputStream(keyApiPropertiesFile))

android {
    namespace Config.namespace

    signingConfigs {
        release {
            // Implements keyStore for release version
        }
    }

    defaultConfig {
        applicationId Config.applicationId
        versionCode Config.versionCode
        versionName Config.versionName
    }

    with flavorConfig
    productFlavors {
        newsapi {
            buildConfigField 'String', 'BASE_URL', '"https://newsapi.org/"'
            buildConfigField "String", "API_KEY", keyApiProperties['NEWS_API_KEY']
        }
        nytimesapi {
            buildConfigField 'String', 'BASE_URL', '"https://api.nytimes.com/"'
            buildConfigField "String", "API_KEY", keyApiProperties['NY_TIMES_API_KEY']
        }
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix '.debug'
        }
        preprod {
            initWith debug
            minifyEnabled true
            applicationIdSuffix '.preprod'
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    "$rootDir/app/proguard-rules.pro",
                    "$rootDir/app/proguard-rules-preprod.pro"
        }
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    "$rootDir/app/proguard-rules.pro",
                    "$rootDir/app/proguard-rules-preprod.pro"
        }
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    tasks.withType(Detekt).configureEach { detekt ->
        detekt.reports {
            xml.required.set(false)
            html.required.set(true)
            txt.required.set(false)
            sarif.required.set(false)
            md.required.set(false)
        }
    }
}

dependencies {
    // Libs
    implementation(Libs.shimmer)
    implementation(Libs.biometric)

    // Modules
    implementation project(Modules.arch)
    implementation project(Modules.design)
    implementation project(Modules.common)

    // Flavors
    newsapiImplementation project(Modules.newsApi)
    nytimesapiImplementation project(Modules.nyTimesApi)
}
